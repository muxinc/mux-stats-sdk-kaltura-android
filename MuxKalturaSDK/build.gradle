plugins {
    id 'com.android.library'
}

apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'com.kezong.fat-aar'

android {
    compileSdk 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdk 24
        targetSdk 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        buildConfigField("String", "MUX_PLUGIN_VERSION", "\"${versionName}\"")
        buildConfigField("String", "MUX_PLUGIN_NAME", "\"android-kaltura-mux\"")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    api 'com.kaltura.playkit:playkit:4.16.0@aar'
    api 'com.kaltura.player:tvplayer:4.16.0'

    // This is due to the problem with fat aar bundling
    // See item #1 https://github.com/kezong/fat-aar-android#known-defects-or-issues
    def muxCoreImport = 'com.mux:stats.muxcore:7.0.2'
    if(project.ext.inLocalDevMode) {
        api muxCoreImport
    } else {
        embed muxCoreImport
    }
}



afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = "com.mux.stats.sdk.muxstats"
                artifactId = "MuxKalturaSDK"
                version = project.ext.versionName
            }
        }
    }
}
