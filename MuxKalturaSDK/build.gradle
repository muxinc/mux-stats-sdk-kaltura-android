apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'com.kezong.fat-aar'

android {
    compileSdk project.ext.compileSdkVersion
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdk project.ext.minSdkVersion
        targetSdk project.ext.targetSdkVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        buildConfigField("String", "MUX_PLUGIN_VERSION", "\"${versionName}\"")
        buildConfigField("String", "MUX_PLUGIN_NAME", "\"android-kaltura-mux\"")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
  namespace 'com.mux.stats.sdk.muxkalturasdk'
}

dependencies {
    api 'com.kaltura.playkit:playkit:4.28.0@aar'
    api 'com.kaltura.player:tvplayer:4.28.0'

    // This is due to the problem with fat aar bundling
    // See item #1 https://github.com/kezong/fat-aar-android#known-defects-or-issues
    def muxCoreImport = 'com.mux:stats.muxcore:7.8.0'
    if(project.ext.inLocalDevMode) {
        api muxCoreImport
    } else {
        embed muxCoreImport
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = "com.mux.stats.sdk.muxstats"
                artifactId = "MuxKalturaSDK"
                version = project.ext.versionName
            }
        }
    }
}
